buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/libs-snapshots-local/' }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
    }
}

plugins {
    id "com.adarshr.test-logger" version "2.0.0"
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    commonBuild = new File(project.rootDir, "gradle").absolutePath
}

subprojects {
    version project.projectVersion

    apply plugin:'groovy'
    apply plugin:'com.adarshr.test-logger'

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }


    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
        testlogger {
            theme 'mocha'
        }
    }
}
